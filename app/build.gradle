apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "uk.co.victoriajanedavis.chatapp"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // For the app private permission used for sending the broadcast and registering the NetworkConnectionReceiver (BNR p.570)
        buildConfigField("String", "APP_PRIVATE_PREMISSION", "\"${chatAppPrivatePermission}\"")
        manifestPlaceholders = [chatAppPrivatePermission:"${chatAppPrivatePermission}"]
    }

    buildTypes {
        // The following debug build settings allowed me to get around a "multidex over 64K methods" error
        // - from: https://developer.android.com/studio/build/shrink-code#gradle-shrinker
        debug {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    final RETROFIT_VERSION = '2.5.0'
    final DAGGER_VERSION = '2.16'
    final MOCKITO_VERSION = '2.6.2'
    final ESPRESSO_VERSION = '3.1.1-alpha01'
    final ROOM_VERSION = '2.1.0-alpha04'
    final LIFECYCLE_VERSION = '2.0.0'
    final NAVIGATION_VERSION = '1.0.0-beta01'
    //final TINDER_SCARLET_VERSION = '0.2.1-alpha4'
    final TINDER_SCARLET_VERSION = '0.2.4-alpha1'
    //final ANDROID_KTX_VERSION = '1.0.1'

    def jUnit = "junit:junit:4.12"
    def mockito = "org.mockito:mockito-core:$MOCKITO_VERSION"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // App Dependencies
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha02'
    implementation 'androidx.annotation:annotation:1.0.1'
    implementation 'com.google.android.material:material:1.1.0-alpha03'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'

    implementation "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$RETROFIT_VERSION"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'

    // Android KTX - Kotlin extensions that are part of Android Jetpack
    //implementation "androidx.core:core-ktx:$ANDROID_KTX_VERSION"

    // Room
    implementation "androidx.room:room-runtime:$ROOM_VERSION"
    implementation "androidx.room:room-rxjava2:$ROOM_VERSION"
    kapt "androidx.room:room-compiler:$ROOM_VERSION"

    // Navigation Component
    implementation "android.arch.navigation:navigation-fragment-ktx:$NAVIGATION_VERSION"
    implementation "android.arch.navigation:navigation-ui-ktx:$NAVIGATION_VERSION"
    // ViewModel + LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$LIFECYCLE_VERSION"
    kapt "androidx.lifecycle:lifecycle-compiler:$LIFECYCLE_VERSION"

    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    // Optional, if you use support library fragments:
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.1'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'

    // Dagger
    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    implementation "com.google.dagger:dagger-android-support:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-android-processor:$DAGGER_VERSION"

    // Tinder/Scarlet (WebSocket library)
    implementation "com.github.tinder.scarlet:scarlet:$TINDER_SCARLET_VERSION"
    implementation "com.github.tinder.scarlet:scarlet-protocol-websocket-okhttp:$TINDER_SCARLET_VERSION"
    implementation ("com.github.tinder.scarlet:scarlet-lifecycle-android:$TINDER_SCARLET_VERSION") {
        exclude group: 'com.github.tinder', module: 'scarlet'
    }
    //implementation "com.github.tinder.scarlet:scarlet-message-adapter-gson:0.2.1-alpha3"
    implementation "com.github.tinder.scarlet:scarlet-message-adapter-gson:$TINDER_SCARLET_VERSION"
    implementation "com.github.tinder.scarlet:scarlet-stream-adapter-rxjava2:$TINDER_SCARLET_VERSION"

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // Instrumentation test dependencies
    androidTestImplementation "androidx.test.ext:junit:1.1.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:$ESPRESSO_VERSION"
    androidTestImplementation mockito
    androidTestImplementation "org.mockito:mockito-android:$MOCKITO_VERSION"
    androidTestImplementation "androidx.arch.core:core-testing:2.0.0"

    // Unit tests dependencies
    testImplementation jUnit
    testImplementation mockito
    testImplementation "org.assertj:assertj-core:3.8.0"
}

apply plugin: 'com.google.gms.google-services'
